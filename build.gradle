plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE'
    id 'io.freefair.lombok' version '4.1.3'
    id 'java'
    id 'application'
}

version = '0.0.1-SNAPSHOT'
mainClassName = project.hasProperty('mainClass') ? getProperty('mainClass') : 'com.kalaazu.Launcher'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation project(':Launcher')
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 12
    group 'com.kalaazu'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    lombok {
        config['lombok.anyConstructor.addConstructorProperties'] = 'true'
    }

    compileJava {
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'
        implementation 'org.reflections:reflections:0.9.11'
    }

    test {
        useJUnitPlatform()
    }
}
