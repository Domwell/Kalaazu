plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "io.freefair.lombok" version "4.1.0"
}

repositories {
    mavenCentral()
    jcenter()
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.kalaazu.main.Launcher'
def configurationFile = 'settings.json'
def watchForChange = '**/*.java'
def doOnChange = './gradlew classes'

shadowJar {
    classifier = 'fat'
    mainClassName = 'io.vertx.core.Launcher'
    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

dependencies {
    compile project(':Launcher')
}

run {
    args = [
            'run',
            mainVerticleName,
            '-conf',
            configurationFile,
            "--redeploy=$watchForChange",
            "--on-redeploy=$doOnChange",
            "--launcher-class=$mainClassName"
    ]
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'

    group 'com.kalaazu'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 12

    repositories {
        mavenCentral()
        mavenLocal()
    }

    lombok {
        version '1.8.10'
    }

    lombok.config['lombok.accessors.fluent'] = 'true'

    compileJava {
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        compile group: 'io.vertx', name: 'vertx-core', version: '4.0.0-milestone4'
        compile group: 'io.vertx', name: 'vertx-unit', version: '4.0.0-milestone3'
        compile group: 'io.netty', name: 'netty-transport-native-epoll', version: '4.1.41.Final', classifier: 'linux-x86_64'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28'
        compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.28'
        compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    }
}
