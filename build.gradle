plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.2'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'application'
}

repositories {
    jcenter()
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.kalaazu.main.Launcher'
def configurationFile = 'settings.json'
def watchForChange = '**/*.java'
def doOnChange = './gradlew classes'

shadowJar {
    classifier = 'fat'
    mainClassName = 'io.vertx.core.Launcher'
    manifest {
        attributes 'Main-Verticle': mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

dependencies {
    compile project(':Launcher')
}

run {
    args = [
            'run',
            mainVerticleName,
            '-conf',
            configurationFile,
            "--redeploy=$watchForChange",
            "--on-redeploy=$doOnChange",
            "--launcher-class=$mainClassName"
    ]
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.franzbecker.gradle-lombok'

    group 'com.kalaazu'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 11

    repositories {
        mavenCentral()
        mavenLocal()
    }

    compileJava {
        doFirst {
            options.compilerArgs = [
                    '--module-path', classpath.asPath,
            ]
            classpath = files()
        }
    }

    lombok {
        version = "1.18.2"
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        compile group: 'io.vertx', name: 'vertx-core', version: '3.5.1'
        compile group: 'io.vertx', name: 'vertx-unit', version: '3.5.1'
        compile group: 'io.netty', name: 'netty-transport-native-epoll', version: '4.1.24.Final', classifier: 'linux-x86_64'
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
        compileOnly 'javax.annotation:javax.annotation-api:1.3.1'
    }
}
